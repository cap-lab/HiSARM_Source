{
    Searcher: Burger burger
    Assistant: Tank tank
    Guard: Waffle waffle
}

Main.Searcher {
    case(REMOTE_CONTROL):
        catch(START): mode = SEARCH("RED", "Guard")
    case(SEARCH):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = HIDE("Guard")
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    case(HIDE):
        catch(RESTART): mode = PREVIOUS_MODE
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    default: mode = REMOTE_CONTROL
}

Main.Assistant {
    case(REMOTE_CONTROL):
        catch(START): mode = FOLLOW
    case(FOLLOW):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = COVER("Guard")
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    case(COVER):
        catch(RESTART): mode = PREVIOUS_MODE
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    default: mode = REMOTE_CONTROL
}

Main.Guard {
    case(REMOTE_CONTROL):
        catch(START): mode = OBSERVE("Searcher", "Assistant")
    case(OBSERVE):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = GUARD("Searcher", "Assistant")
    case(GUARD):
        catch(RESTART): mode = PREVIOUS_MODE
    default: mode = REMOTE_CONTROL
}

Mode.REMOTE_CONTROL {
    services:
        RemoteControlGroup
        ReceiveCommandFromOperator
        SendImageToOperator
}

Mode.SEARCH(target, sender) {
    services:
        Search(target)
        ListenAlarm(sender)
        ReceiveCommandFromOperator
}

Mode.HIDE(sender) {
    services:
        Hide
        ListenAlarm(sender)
        ReceiveCommandFromOperator
}

Mode.FOLLOW {
    services:
        Follow
        ReceiveCommandFromOperator
}

Mode.COVER(sender) {
    services:
        Attack
        ListenAlarm(sender)
        ReceiveCommandFromOperator
}

Mode.OBSERVE(receiver1, receiver2) {
    services:
        ObserveAndShare(receiver1, receiver2)
        ReceiveCommandFromOperator
}

Mode.GUARD(receiver1, receiver2) {
    services:
        ReleaseAlarm(receiver1, receiver2)
        Attack
        ReceiveCommandFromOperator
}

RemoteControlGroup {
    direction = getDirection()
    moveByRemoteControl(direction)
} repeat(100 MSEC)

ReceiveCommandFromOperator {
    cmd = getModeTrasferCommand()
    if (cmd == "REMOTE_CONTROL"){
        throw REMOTE_CONTROL broadcast
    } else if (cmd == "START") {
        throw START broadcast
    } else if (cmd == "FINISH") {
        throw FINISH broadcast
    }
} repeat(500 MSEC)

SendImageToOperator {
    sendImageToOperator()
} repeat(100 MSEC)

Search(target) {
    isFound = search(target)
    if (isFound == "TRUE") {
        throw FINISH broadcast
    }
}

Hide {
    hide()
}

Follow {
    follow()
}

Attack {
    attack()
}

ObserveAndShare(receiver1, receiver2) {
    isEnemy = checkEnemy()
    if (isEnemy == "TRUE") {
        send(receiver1, isEnemy)
        send(receiver2, isEnemy)
        throw HIDE broadcast
    }
}

ReleaseAlarm(receiver1, receiver2) {
    isEnemy = checkEnemy()
    if (isEnemy == "FALSE") {
        send(receiver1, isEnemy)
        send(receiver2, isEnemy)
        throw RESTART broadcast
    }
}

ListenAlarm(sender) {
    isEnemy = receive(sender, isEnemy)
    if (isEnemy == "TRUE") {
        throw HIDE broadcast
    }
    if (isEnemy == "FALSE") {
        throw RESTART broadcast
    }
}
