{
    Searcher: Burger burger
    Assistant: Tank tank
    Guard: Waffle waffle
}

Main.Searcher {
    case(REMOTE_CONTROL):
        catch(START): mode = SEARCH("RED", "Guard")
    case(SEARCH):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = HIDE("Guard")
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    case(HIDE):
        catch(RESTART): mode = PREVIOUS_MODE
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    default: mode = REMOTE_CONTROL
}

Main.Assistant {
    case(REMOTE_CONTROL):
        catch(START): mode = FOLLOW
    case(FOLLOW):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = COVER("Guard")
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    case(COVER):
        catch(RESTART): mode = PREVIOUS_MODE
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
    default: mode = REMOTE_CONTROL
}

Main.Guard {
    case(REMOTE_CONTROL):
        catch(START): mode = WATCH("Searcher", "Assistant")
    case(WATCH):
        catch(FINISH): mode = FINISH
        catch(HIDE): mode = GUARD("Searcher", "Assistant")
    case(GUARD):
        catch(RESTART): mode = PREVIOUS_MODE
    default: mode = REMOTE_CONTROL
}

Mode.REMOTE_CONTROL {
    services:
        RemoteControlGroup
        ReceiveCommand
        SendImage
}

Mode.SEARCH(target, sender) {
    services:
        Search(target)
        ListenAlarm(sender)
        ReceiveCommand
}

Mode.HIDE(sender) {
    services:
        Hide
        ListenAlarm(sender)
        ReceiveCommand
}

Mode.FOLLOW {
    services:
        Follow
        ReceiveCommand
}

Mode.COVER(team_name) {
    services:
        Attack
        ListenAlarm(team_name)
        ReceiveCommand
}

Mode.WATCH(receiver1, receiver2) {
    services:
        Watch(receiver1, receiver2)
        ReceiveCommand
}

Mode.GUARD(receiver1, receiver2) {
    services:
        ReleaseAlarm(receiver1, receiver2)
        Attack
        ReceiveCommand
}

RemoteControlGroup {
    direction = group_remote_control()
    group_move(direction)
} repeat(100 MSEC)

ReceiveCommand {
    cmd = group_get_cmd()
    if (cmd == "REMOTE_CONTROL"){
        throw REMOTE_CONTROL broadcast
    } else if (cmd == "START") {
        throw START broadcast
    } else if (cmd == "FINISH") {
        throw FINISH broadcast
    }
} repeat(500 MSEC)

SendImage {
    sendImage()
} repeat(100 MSEC)

Search(target) {
    search(target)
}

Hide {
    hide()
}

Follow {
    follow()
}

Attack {
    attack()
}

ListenAlarm(sender) {
    ISENEMY = receive(sender, ISENEMY)
    if (ISENEMY == "TRUE") {
        throw HIDE broadcast
    }
    if (ISENEMY == "FALSE") {
        throw RESTART broadcast
    }
}

Watch(receiver1, receiver2) {
    ISENEMY = checkEnemy()
    if (ISENEMY == "TRUE") {
        send(receiver1, ISENEMY)
        send(receiver2, ISENEMY)
        throw HIDE broadcast
    }
}

ReleaseAlarm(receiver1, receiver2) {
    ISENEMY = checkEnemy()
    if (ISENEMY == "FALSE") {
        send(receiver1, ISENEMY)
        send(receiver2, ISENEMY)
        throw RESTART broadcast
    }
}