{
    Robot: Burger burger[6]
}

Main.Robot {
    case(REMOTE_CONTROL):
        catch(START): mode = SEARCH_GROUPING
        catch(FINISH): mode = FINISH
    case(SEARCH_GROUPING):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(FINISH): mode = FINISH
    default: mode = REMOTE_CONTROL
}


Mode.REMOTE_CONTROL {
    services:
        RemoteControl
        SendImageToOperator
        ReceiveCommandFromOperator
}


Mode.SEARCH_GROUPING {
    group(groupA, min=2):
        ModeTransition.SEARCH("R1", "YELLOW")
    group(groupB, min=2, proper=4):
        ModeTransition.SEARCH("R2", "YELLOW")
    services:
        ReceiveCommandFromOperator
}

ModeTransition.SEARCH(area, target) {
    case(MOVE_TO_MISSION_AREA):
        catch(ARRIVED): mode = SEARCH
    default: mode = MOVE_TO_MISSION_AREA(area)
}


Mode.MOVE_TO_MISSION_AREA(area) {
    services:
        MoveTo(area)
}

Mode.SEARCH {
    services:
        Search("YELLOW")
}

RemoteControl {
    remoteControl()
}

ReceiveCommandFromOperator {
    cmd = getModeTrasferCommand()
    if (cmd == "RC"){
        throw REMOTE_CONTROL
    } else if (cmd == "START") {
        throw START
    } else if (cmd == "FINISH") {
        throw FINISH
    }
} 

SendImageToOperator {
    sendImageToOperator()
}

Search(target) {
    isFound = search(target)
    if (isFound == "TRUE") {
        throw FINISH broadcast
    }
} repeat(200 MSEC)

MoveTo(area) {
    arrive = moveTo(area)
    if (arrive == "TRUE") {
        throw ARRIVED
    }
} repeat(200 MSEC)



