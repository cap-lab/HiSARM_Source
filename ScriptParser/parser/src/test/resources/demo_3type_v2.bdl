{
    Searcher: Burger burger
    Assistant: Tank tank
    Guard: Waffle waffle
}

Main.Searcher {
    case(REMOTE_CONTROL):
        catch(START): mode = SEARCH("YELLOW", "Guard")
        catch(FINISH): mode = FINISH
    case(SEARCH):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(HIDE): mode = HIDE("Guard")
        catch(FINISH): mode = FINISH
    case(HIDE):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(RESTART): mode = SEARCH
    default: mode = REMOTE_CONTROL
}

Main.Assistant {
    case(REMOTE_CONTROL):
        catch(START): mode = FOLLOW
        catch(FINISH): mode = FINISH
    case(FOLLOW):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(HIDE): mode = COVER("Guard")
        catch(FINISH): mode = FINISH
    case(COVER):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(RESTART): mode = FOLLOW
    default: mode = REMOTE_CONTROL
}

Main.Guard {
    case(REMOTE_CONTROL):
        catch(START): mode = OBSERVE("Searcher", "Assistant")
        catch(FINISH): mode = FINISH
    case(OBSERVE):
        catch(HIDE): mode = GUARD("Searcher", "Assistant")
        catch(FINISH): mode = FINISH
    case(GUARD):
        catch(REMOTE_CONTROL): mode = REMOTE_CONTROL
        catch(RESTART): mode = OBSERVE
    default: mode = REMOTE_CONTROL
}

Mode.REMOTE_CONTROL {
    services:
        RemoteControl
        SendImageToOperator
        ReceiveCommandFromOperator
}

Mode.SEARCH(target, sender) {
    services:
        Search(target)
        ListenAlarmFrom(sender)
        ReceiveCommandFromOperator
}

Mode.HIDE(sender) {
    services:
        Hide
        ListenAlarmFrom(sender)
        ReceiveCommandFromOperator
}

Mode.FOLLOW {
    services:
        Follow
        ListenAlarmFrom(sender)
        ReceiveCommandFromOperator
}

Mode.COVER(sender) {
    services:
        Attack
        ListenAlarmFrom(sender)
        ReceiveCommandFromOperator
}

Mode.OBSERVE(receiver1, receiver2) {
    services:
        ObserveAndShareWith(receiver1, receiver2)
        ReceiveCommandFromOperator
}

Mode.GUARD(receiver1, receiver2) {
    services:
        TurnOffAlarmOn(receiver1, receiver2)
        Attack
        ReceiveCommandFromOperator
}

RemoteControl {
    remoteControl()
} repeat(100 MSEC)

ReceiveCommandFromOperator {
    cmd = getModeTrasferCommand()
    if (cmd == "RC"){
        throw REMOTE_CONTROL broadcast
    } else if (cmd == "START") {
        throw START broadcast
    } else if (cmd == "FINISH") {
        throw FINISH broadcast
    }
} repeat(100 MSEC)

SendImageToOperator {
    sendImageToOperator()
} repeat(100 MSEC)

Search(target) {
    isFound = search(target)
    if (isFound == "TRUE") {
        throw FINISH broadcast
    }
} repeat(100 MSEC)

Hide {
    hide()
} repeat(100 MSEC)

Follow {
    follow()
} repeat(100 MSEC)

Attack {
    attack()
} repeat(100 MSEC)

ObserveAndShareWith(receiver1, receiver2) {
    isEnemy = checkEnemy()
    if (isEnemy == "TRUE") {
        send(receiver1, isEnemy)
        send(receiver2, isEnemy)
        throw HIDE broadcast
    }
}

TurnOffAlarmOn(receiver1, receiver2) {
    isEnemy = checkEnemy()
    if (isEnemy == "FALSE") {
        send(receiver1, isEnemy)
        send(receiver2, isEnemy)
        throw RESTART broadcast
    }
}

ListenAlarmFrom(sender) {
    isEnemy = receive(sender, isEnemy)
    if (isEnemy == "TRUE") {
        throw HIDE broadcast
    }
    if (isEnemy == "FALSE") {
        throw RESTART broadcast
    }
} 
